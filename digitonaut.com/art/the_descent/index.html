<!DOCTYPE html>

<!-- Mirrored from digitonaut.com/art/the_descent/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 11:36:39 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>The Descent</title>

<script src="shader-web-background.min.js"></script>
<script type="x-shader/x-fragment" id="Image">
precision highp float;
uniform vec2  iResolution;
uniform float iTime;

// -------------------------------------------------------------------------------------------------- Shadertoy code
const float PI=3.14159265358979323846;

#define speed (iTime*0.5)
#define value (0.4+0.25*cos(PI*speed*0.125))

vec2 rotate(vec2 k,float t)
	{
	return vec2(cos(t)*k.x-sin(t)*k.y,sin(t)*k.x+cos(t)*k.y);
	}

float scene1(vec3 p)
	{
	float dot_p=0.125;
	float dot_w=dot_p*0.625;
	float dot=length(mod(p.xyz,dot_p)-dot_p*0.5)-dot_w;
	float ball_p=1.0;
	float ball_w=ball_p*(0.6625-0.075*(1.0-value));
	float ball=length(mod(p.xyz,ball_p)-ball_p*0.5)-ball_w;
	float hole_w=ball_p*(0.625+0.0125*value);
	float hole=length(mod(p.xyz,ball_p)-ball_p*0.5)-hole_w;
	float hole2_p=0.125;
	float hole2_w=hole2_p*0.375;
	float hole2=length(mod(p.xyz,hole2_p)-hole2_p*0.5)-hole2_w;
	return max(max(dot,-mix(hole,hole2,0.5)),ball);
	}

void mainImage(out vec4 fragColor,in vec2 fragCoord)
	{
	vec2 position=(fragCoord.xy/iResolution.xy);
	vec2 p=-1.0+2.0*position;
	vec3 vp=normalize(vec3(p*vec2(1.77,1.0),0.75)); // screen ratio (x,y) fov (z)
	//vp.yz=rotate(vp.yz,PI*0.125*sin(speed*0.5));	// rotation x
	//vp.zx=rotate(vp.zx,PI*0.125*sin(speed*0.5));	// rotation y
	vp.xy=rotate(vp.xy,speed*0.25);					// rotation z
	float ground_x=0.0;//1.0+0.05*cos(PI*speed*0.25);
	float ground_y=0.0;//1.0+0.05*sin(PI*speed*0.25);
	float ground_z=speed*(0.125+0.375);
	vec3 ray=vec3(ground_x,ground_y,ground_z);
	float t=0.0;
	const int ray_n=96;
	for(int i=0;i<ray_n;i++)
		{
		float k=scene1(ray+vp*t);
        if(abs(k)<0.002) break;
		t+=k*0.7;
		}
	vec3 hit=ray+vp*t;
	vec2 h=vec2(-0.1,0.1); // light
	vec3 n=normalize(vec3(scene1(hit+h.xyy),scene1(hit+h.yxx),scene1(hit+h.yyx)));
	float c=(n.x+n.y+n.z)*0.08+t*0.16;
	float color=-0.25*cos(PI*position.x*2.0)+0.25*sin(PI*position.y);
	fragColor=vec4(vec3((c*t+t)*0.5*value+color,c*1.5-t*0.025,c*1.25-t*0.0125*value-color)*c,1.0);
	}
// -------------------------------------------------------------------------------------------------- End of Shadertoy code

void main() {
	mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	shaders: {
		Image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/the_descent/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 11:36:41 GMT -->
</html>
